import '/os'
import '/random/choice'

import '/flask/Flask'
import '/flask/render_template'
import '/flask/send_from_directory'
import '/jinja2/MemcachedBytecodeCache'
import '/werkzeug/contrib/cache/SimpleCache'

import '../config'
import '../database/Client'


Yoboard = subclass $ Flask where
  __init__ = *: a **: k ~>
    Flask.__init__ self *: a **: k

    @config.from_object config
    @jinja_env.trim_bullshit  = not @debug
    @jinja_env.bytecode_cache = MemcachedBytecodeCache SimpleCache!
    @jinja_env.add_extension  $ import 'bullshit/BullshitExtension'
    @jinja_env.globals.update $ dict'
      'random_banner', ->
        xs = os.listdir config.BANNER_DIR
        xs => choice xs

    @add_url_rule '/static/upload/<path:filename>' 'upload' $ bind send_from_directory config.UPLOAD_DIR as_attachment: True
    @add_url_rule '/static/banner/<path:filename>' 'banner' $ bind send_from_directory config.BANNER_DIR as_attachment: True

    @register_blueprint $ import 'core/mod'
    @register_blueprint $ import 'admin/mod'
    @register_error_handler 400 @onerror
    @register_error_handler 403 @onerror
    @register_error_handler 404 @onerror
    @register_error_handler 500 @onexcept

  relative = p ~> os.path.relpath p $ os.path.dirname config.__file__
  onexcept = e ~> (render_template 'error.hamlish' err: e code: 500    relative: @relative, 500)
  onerror  = e ~> (render_template 'error.hamlish' err: e code: e.code relative: @relative, e.code)

data = Client  config.CLIENT_ADDRESS
app  = Yoboard __name__
