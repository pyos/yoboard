import '/flask/Blueprint'
import '/flask/redirect'
import '/flask/request'
import '/flask/render_template'
import '/flask/url_for'

import 'data'
import 'config'
import 'tools/make_post'
import 'tools/mimeroute'
import 'tools/jsreturn'


CONFIG = dict $ map (k -> (k, getattr config k)) $ set'
  'SITE_NAME'
  'SITE_TITLE'
  'POSTS_UNTIL_AUTOSAGE'
  'POSTS_PER_THREAD'
  'MAX_TITLE_LENGTH'
  'MAX_TEXT_LENGTH'
  'MAX_TEXT_LINES'
  'MAX_UPLOADS'
  'NOT_EMPTY'
  'IMAGES_ALWAYS_BUMP'


root = mimeroute $ isjson -> if
  isjson    => jsreturn $ dict config: CONFIG boards: data.boards!
  otherwise => render_template 'root.hamlike' boards: data.boards!


board = mimeroute $ isjson board page: 0 -> if
  isjson    => jsreturn $ dict $ zip ('title', 'threads', 'pages') $ data.board board page
  otherwise =>
    data.get_cached_board board page or
      result = render_template 'board.hamlike'
        this:   (data.board board page)
        page:    page
        board:   board
      data.set_cached_board board page result
      result


thread = mimeroute $ isjson board thread -> if
  isjson    => jsreturn $ data.thread board thread
  otherwise =>
    view = request.cookies.get 'view_type'
    redirect $ url_for board: board thread: thread $ if
      view == 'tree' => '.thread_tree'
      otherwise      => '.thread_list'


thread_render = t view -> board thread ->
  data.get_cached_thread board thread t or
    result = render_template 'thread.hamlike'
      this: (data.thread board thread)
      view:  view
      board: board
    data.set_cached_thread board thread t result
    result


post = mimeroute $ isjson board id ->
  isjson    => jsreturn $ data.post board id
  otherwise => render_template 'post.1.hamlike'
    this:  (data.post board id)
    board:  board


post_create = board ->
  redirect $ url_for '.post' board: board id:
    make_post board
      int  $ request.form.get 'parent' 0
      bool $ request.form.get 'sage'   False
      request.form.get      'title' ''
      request.form.get      'text'  ''
      request.files.getlist 'file'


mod = Blueprint 'core' __name__
mod.add_url_rule '/'                           None root
mod.add_url_rule '/<board>/'                   None post_create methods: (list' 'POST')
mod.add_url_rule '/<board>/'                   None board
mod.add_url_rule '/<board>/page/<int:page>/'   None board
mod.add_url_rule '/<board>/post/<int:id>/'     None post
mod.add_url_rule '/<board>/<int:thread>/'      None thread
mod.add_url_rule '/<board>/<int:thread>/tree/' 'thread_tree' $ thread_render 0 'tree'
mod.add_url_rule '/<board>/<int:thread>/list/' 'thread_list' $ thread_render 1 'list'
mod.add_url_rule '/api/'                       'json' $ -> render_template 'api.hamlike'
