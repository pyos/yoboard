import '/socket'
import '/bluelet'
import '/pickle/loads'
import '/pickle/dumps'

NOP  = b'N'  # Same as `b''`, but with a defined `!! -1`.
STOP = 46    # End of a single pickled object.


Server = (host, port) commands -> bluelet.server host port $ conn ->
  while True =>
    buffer = NOP
    while buffer !! -1 != STOP =>
      buffer += indata = yield $ conn.recv 8192
      not indata => raise StopIteration

    yield $ conn.sendall $ dumps $ except
      e =>
        name, args = loads buffer
        (commands !! name) *: args
      e => e


Client = subclass object where
  __init__  = @addr ~>
  reconnect = ~>
    @socket = socket.socket socket.AF_INET socket.SOCK_STREAM
    @socket.connect @addr

  __getattr__ = name ~> *: args ->
    @socket.sendall $ dumps (name, args)

    buffer = NOP
    while buffer !! -1 != STOP =>
      buffer += indata = @socket.recv 8192
      not indata => raise $ IOError 'database connection lost'

    response = loads buffer
    response :: Exception => raise response
    response
