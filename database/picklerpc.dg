import '/socket'
import '/select/select'
import '/pickle/dumps'
import '/pickle/Unpickler'


Server = (family, addr) commands ->
  server = socket.socket family
  server.bind addr
  server.listen 10
  read = dict' (server, None)

  while True => for s in select read list! list! !! 0 => if
    s is server =>
      sock = fst server.accept!
      read !! sock = Unpickler $ sock.makefile 'rb' buffering: 8192

    otherwise => except
      e =>
        name, args = (read !! s).load!
        s.sendall $ dumps $ (commands !! name) *: args
      e :: EOFError             => read !!~ s
      e :: ConnectionResetError => read !!~ s
      e :: Exception            => s.sendall $ dumps e


Client = subclass object where
  __init__  = (@family, @addr) ~>
  reconnect = ~>
    @socket = socket.socket @family
    @socket.connect @addr
    @loader = Unpickler $ @socket.makefile 'rb' buffering: 8192

  __getattr__ = name ~> *: args ->
    response = @call name args
    response :: Exception => raise response
    response

  call = name args ~> except
    e =>
      @socket.sendall $ dumps (name, args)
      @loader.load!
    e :: OSError and e.errno in (32, 107) =>
      @reconnect
      @call name args
