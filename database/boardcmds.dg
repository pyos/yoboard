import '/os'
import '/time'
import '/collections/deque'
import '/collections/defaultdict'

import 'Post'
import 'Tree'
import 'Board'
import 'Thread'
import '../config'


remove_uploads = obj -> for (fn, thumb, *_) in obj.files =>
  fn    => except
    err => os.unlink $ os.path.join config.UPLOAD_DIR fn
    err :: OSError =>  # does not exist or is not in a writable directory
  thumb => except
    err => os.unlink $ os.path.join config.UPLOAD_DIR thumb
    err :: OSError =>  # fix that yourself


commands = data ->
  RP_COUNT  = data !! 'ids'       # :: {str: int}
  RP_BOARD  = data !! 'boards'    # :: {str: Board}
  RP_CATMAP = data !! 'boardmap'  # :: {str: {str: str}}
  RP_TREES  = data !! 'trees'     # :: {str: {int: Either Thread Tree}}
  RP_USERS  = data !! 'users'     # :: {str: Either [str] bool}

  # An {str: int} mapping that contains the number of attached threads
  # on each board. Note that it's not necessary to store that in the file.
  VD_ATTACHED = dict (None where for (n, b) in RP_BOARD.items! =>
    yield (n, sum $ map (x -> x.attached) b.threads))

  # Volatile cache containers.
  # These are in the database because that
  #   1. allows flask workers to share cache;
  #   2. makes invalidating them easier.
  VC_BOARD  = defaultdict dict  # :: {str: {int: str}}
  VC_THREAD = defaultdict dict  # :: {(str, int): {int: str}}

  post_cleanup = None
  post_cleanup = board num ->
    tree   = (RP_TREES !! board).pop num
    thread = (RP_TREES !! board).get tree.root.thread None
    thread => if tree.root in thread.latest => thread.latest.remove tree.root
                 otherwise                  => thread.skipped -= 1
    remove_uploads tree.root
    for subtree in tree.replies => post_cleanup board subtree.root.id

  writes  = set' 'user_set' 'board_create' 'thread_create'
  writes |= set' 'thread_set_closed' 'thread_set_attached' 'post_create'
  writes |= set' 'board_remove' 'post_remove'
  writes, dict'
    # :: -> ()
    'clear_cache', ->
      VC_BOARD.clear!
      VC_THREAD.clear!

    # :: str int     -> Maybe str
    # :: str int str -> ()
    'get_cached_board', name page   -> (VC_BOARD !! name).get page
    'set_cached_board', name page x -> (VC_BOARD !! name).__setitem__ page x

    # :: str int int     -> Maybe str
    # :: str int int str -> ()
    'get_cached_thread', board num vt   -> (VC_THREAD !! (board, num)).get vt
    'set_cached_thread', board num vt x -> (VC_THREAD !! (board, num)).__setitem__ vt x

    # :: str            -> Either [str] bool
    # :: str (Either [str] bool) -> ()
    'user',     uid        -> RP_USERS.get uid False
    'user_set', uid boards -> RP_USERS.__setitem__ uid boards

    # ::         -> {str: {str: str}}
    # :: str int -> Post
    # :: str int -> Thread
    'boards',           -> RP_CATMAP
    'post',   board num -> (RP_TREES !! board !! num).root
    'thread', board num ->
      th = RP_TREES !! board !! num
      th :: Tree => raise $ KeyError num
      th

    # Fetch a list of threads on a board.
    # Also returns the title of the board and the number of pages.
    #
    # :: str int -> (str, [Thread], int)
    #
    'board', name page ->
      board    = RP_BOARD !! name
      pages, r = len board.threads `divmod` config.THREADS_PER_PAGE
      pages < page => raise $ KeyError page

      data = None where
        n = page * config.THREADS_PER_PAGE
        for t in board.threads !! slice n (n + config.THREADS_PER_PAGE) =>
          # To reduce the amount of data transferred to the web server,
          # reply trees are removed.
          yield $ Thread t.root None t.latest t.skipped t.attached t.closed

      board.title, list data, pages + bool r

    # Create a new board or update the old one.
    #
    # :: str str str -> ()
    #
    'board_create', name title category ->
      if
        name in RP_BOARD =>
          old = RP_BOARD !! name
          RP_BOARD  !! name = Board name title category old.threads
          RP_CATMAP !! old.category !!~ name
          RP_CATMAP !! old.category or RP_CATMAP !!~ old.category
        otherwise =>
          RP_COUNT !! name = 0
          RP_BOARD !! name = Board name title category list!
          RP_TREES !! name = dict!
          VD_ATTACHED !! name = 0
      RP_CATMAP !! category = RP_CATMAP.get category dict!
      RP_CATMAP !! category !! name = title
      None

    # Create a new thread, return its ID.
    #
    # :: str str bool str [(str, str)] str -> int
    #
    'thread_create', board title sage text files ip ->
      boardo = RP_BOARD !! board
      rootid = RP_COUNT !! board += 1
      rootts = int $ time.mktime time.gmtime!
      root   = Post rootid title sage text files rootid 0 rootts ip
      thread = Thread root list! list! 0 False False

      boardo.threads.insert (VD_ATTACHED !! board) thread
      config.THREADS_PER_BOARD < len boardo.threads =>
        cn = boardo.threads.pop -1
        post_cleanup board cn.root.id

      VC_BOARD.pop board None  # All pages are invalid now.
      RP_TREES !! board !! rootid = thread
      rootid

    # Set the `closed` flag of a thread.
    # Closed threads cannot be replied to.
    # Note that this is independent of a reply count limit.
    #
    # :: str int bool -> ()
    #
    'thread_set_closed', board num state ->
      state  = bool state
      thread = RP_TREES !! board !! num
      if thread :: Tree         => raise KeyError
         thread.closed is state => None
         otherwise              =>
           thread.closed = bool state
           # Since there's probably some kind of icon for that,
           # the cache is now invalid.
           VC_BOARD.pop   board       None
           VC_THREAD.pop (board, num) None
           None

    # Set the `attached` flag of a thread.
    # Attached threads always appear at the top of the first page.
    #
    # :: str int bool -> ()
    #
    'thread_set_attached', board num state ->
      state  = bool state
      boardo = RP_BOARD !! board
      thread = RP_TREES !! board !! num
      if thread :: Tree           => raise KeyError
         thread.attached is state => None
         otherwise =>
           thread.attached = state
           VD_ATTACHED !! board -= not state
           boardo.threads.remove                        thread
           boardo.threads.insert (VD_ATTACHED !! board) thread
           VD_ATTACHED !! board += state
           VC_BOARD.pop   board       None
           VC_THREAD.pop (board, num) None
           None

    # Create a new reply, return its ID.
    #
    # :: str int str bool str [(str, str)] str -> int
    #
    'post_create', board parent title sage text files ip ->
      boardobj  = RP_BOARD !! board
      treeobj   = RP_TREES !! board !! parent
      parentobj = treeobj.root
      threadobj = RP_TREES !! board !! parentobj.thread

      threadlen = len threadobj.latest + threadobj.skipped
      threadobj.closed                         => raise OverflowError
      threadlen >= config.POSTS_PER_THREAD     => raise OverflowError
      threadlen >= config.POSTS_UNTIL_AUTOSAGE =>
        config.IMAGES_ALWAYS_BUMP and files => raise OverflowError
        sage = True

      postid  = RP_COUNT !! board += 1
      postts  = int $ time.mktime time.gmtime!
      postobj = Post postid title sage text files parentobj.thread parent postts ip
      curtree = Tree postobj list!

      RP_TREES !! board !! postid = curtree
      treeobj.replies.append curtree
      threadobj.latest.append postobj

      len threadobj.latest > config.POSTS_ON_BOARD_PAGE =>
        threadobj.latest.pop 0
        threadobj.skipped += 1

      not ((sage and not (files and config.IMAGES_ALWAYS_BUMP)) or threadobj.attached) =>
        boardobj.threads.remove                        threadobj
        boardobj.threads.insert (VD_ATTACHED !! board) threadobj

      VC_BOARD.pop   board                  None
      VC_THREAD.pop (board, postobj.thread) None
      postid

    # :: str -> ()
    'board_remove', name ->
      board = RP_BOARD.pop name
      for p in (RP_TREES !! name).values! => remove_uploads p.root
      RP_CATMAP !! board.category !!~ name
      RP_CATMAP !! board.category or RP_CATMAP !!~ board.category
      RP_TREES !!~ name
      RP_COUNT !!~ name
      VD_ATTACHED !!~ name
      VC_BOARD.pop name None
      VC_THREAD.clear!
      None

    # :: str int -> ()
    'post_remove', board num ->
      curtree = RP_TREES !! board !! num
      if
        curtree.root.parent =>
          treeobj = RP_TREES !! board !! curtree.root.parent
          treeobj.replies.remove curtree
        otherwise =>
          boardobj = RP_BOARD !! board
          boardobj.threads.remove curtree
          VD_ATTACHED !! board -= curtree.attached

      post_cleanup board num
      VC_BOARD.pop   board                       None
      VC_THREAD.pop (board, curtree.root.thread) None
      None
